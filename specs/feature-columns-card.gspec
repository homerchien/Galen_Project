@import data/common-data.gspec
@import data/feature-columns-data.gspec
@script properties/feature-columns.properties.js

= Feature Columns Card =
    = Skeleton =
        ${ele_container}:
            @on desktop
                width ${desktop_container_width_xl}
            @on mobile
                width 100% of screen/width
        | amount of any ${ele_only_items} should be ${block_quantity}
            
    = Text =
        ${ele_title}:
            text is "${conf_settings_title}"
            inside ${ele_title_row}
        | ${ele_title} fulfil common pattern rules follow by ${css_check_ele_title}

        
    = Items =
        = Skeleton =
            @on desktop
                @if ${ block_quantity < 3 || block_quantity == 4 }
                    | ${ele_only_items} should be inside of ${ele_product_row} and divide in 2 equally by 0 px fixed space
                    | ${ele_only_items} should be 2 in a row with 24px horizontal gap and 24px vertical gap
                @elseif ${ block_quantity == 3 || block_quantity > 5}
                    | ${ele_only_items} should be inside of ${ele_product_row} and divide in 3 equally by 0 px fixed space
                    | ${ele_only_items} should be 3 in a row with 24px horizontal gap and 24px vertical gap
                @elseif ${ block_quantity == 5}
                    | ${ele_only_items_2_of_5} should be inside of ${ele_product_row} and divide in 2 equally by 0 px fixed space
                    | ${ele_only_items_3_of_5} should be inside of ${ele_product_row} and divide in 3 equally by 0 px fixed space
                    | ${ele_only_items_2_of_5} are aligned horizontally top next to each other with 0px margin
                    | ${ele_only_items_3_of_5} are aligned horizontally top next to each other with 0px margin

            @on mobile
                | ${ele_only_items} should be inside of ${ele_product_row} and take 100 % of its width after subtracting 0 px extra-margin
                | ${ele_only_items} should be 1 in a row with 0px horizontal gap and ~32px vertical gap

        = Image =
            @on desktop
                | ${ele_only_items_image} inside of ${ele_only_items} and ~12px left right of its separately
            @on mobile
                | ${ele_only_items_image} inside of ${ele_only_items} and ~8px left right of its separately
            | ${ele_only_items_image} inside of ${ele_only_items} and 0px top of its separately


        = Title =
            @on desktop
                | ${ele_only_items_title} inside of ${ele_only_items} and ~12px left right of its separately
            @on mobile
                | ${ele_only_items_title} inside of ${ele_only_items} and ~8px left right of its separately
            | ${ele_only_items_title} fulfil text is the value of settings.title from ${arrayToString(conf_blocks)} by one-to-one mapping
            | ${ele_only_items_title} fulfil common pattern rules follow by ${css_check_ele_items_title}

        = Describe =
            @on desktop
                | ${ele_only_items_text} inside of ${ele_only_items} and ~12px left right of its separately
            @on mobile
                | ${ele_only_items_text} inside of ${ele_only_items} and ~8px left right of its separately
            | ${ele_only_items_text} inside of ${ele_only_items} and 18px bottom of its separately
            | ${ele_only_items_text} fulfil text is the value of settings.text from ${arrayToString(conf_blocks)} by one-to-one mapping
            | ${ele_only_items_text} fulfil common pattern rules follow by ${css_check_ele_items_text}
                

        


