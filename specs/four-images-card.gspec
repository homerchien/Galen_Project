@import data/common-data.gspec
@import data/four-images-data.gspec
@script properties/four-images.properties.js

= Four Images Card =
    = Skeleton =
        @on desktop
            ${ele_container}:
                width ${desktop_container_width_xl}
            | ${ele_container} fulfil common pattern rules follow by ${desktop_css_check_container_gap_padding}
        @on mobile
            ${ele_container}:
                width 100% of screen/width
            | ${ele_container} fulfil common pattern rules follow by ${mobile_css_check_container_gap_padding}
        
        ${ele_top_left}:
            aligned horizontally top ${ele_only_top_right_image}
            aligned vertically left ${ele_bottom}
        @on desktop
            = Left Image =
                | ${ele_top_left} should be inside of ${ele_top} and take 68 to 69 % of its width
            = Right Image =
                | ${ele_top_right} should be inside of ${ele_top} and take 31 to 32 % of its width
            = Bottom Image =
                ${ele_bottom}:
                    width 68 to 69 % of ${ele_top}/width
        @on mobile
            = Left Image =
                ${ele_top_left}:
                    width 50% of ${ele_top}/width
            = Right Image =
                | ${ele_top_right} should be inside of ${ele_top} and take 50 % of its width
            = Bottom Image =
                ${ele_bottom}:
                    width 100 % of ${ele_top}/width

    = Image =
        = Left Image =
            @on desktop
                | ${ele_only_top_left_image} are aligned horizontally all next to each other with ~0px margin
                | ${ele_only_top_left_image} should be inside of ${ele_top_left} and take 50 % of its width

            @on mobile
                | ${ele_only_top_left_image} are aligned vertically all above each other
                | ${ele_only_top_left_image} should be inside of ${ele_top_left} and take 95 to 96 % of its width
